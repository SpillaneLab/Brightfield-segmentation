	//This macro segments on the BF channel and references the calcium channel

	//Select all of the relevant directories 
   
   dir = getDirectory("Choose an input directory");
   outputDir = getDirectory("Select an output directory");
   setBatchMode(true);
   count = 0;
   countFiles(dir);
   n = 0;
   processFiles(dir);

   //Open all of the files within subfolders 
   
   
   function countFiles(dir) {
      list = getFileList(dir);
      for (i=0; i<list.length; i++) {
          if (endsWith(list[i], "/"))
              countFiles(""+dir+list[i]);
          else
              count++;
      }
  }

   function processFiles(dir) {
      list = getFileList(dir);
      for (i=0; i<list.length; i++) {
          if (endsWith(list[i], "/"))
              processFiles(""+dir+list[i]);
          else {
             showProgress(n++, count);
             path = dir+list[i];
             processFile(path);
          }
      }
  }

  function processFile(path) {
      //Open the image
      open(path);
      
      //Get the file name 
      imagesName=getTitle();
       		
      //Set the image properties
      getDimensions(width, height, channels, slices, frames);
			run("Properties...", "channels=channels slices=slices frames=frames unit=Âµm pixel_width=0.11 pixel_height=0.11 voxel_depth=1.0000000");
			
      //Duplicate the original image and rename
      run("Duplicate...", "duplicate");
      rename("Image");

      //Split the channels 
      run("Split Channels");
       		
      //Segment on the brightfield channel 
      selectWindow("C6-Image");
      run("Duplicate...", "duplicate");
      rename("BF");
      run("Smooth");
      run("Maximum...", "radius=5");
			run("Gaussian Blur...", "sigma=8");
			run("Auto Threshold", "method=Triangle white");
			run("Convert to Mask");
			run("Fill Holes");
			run("Watershed");
			
			//Set measurements and analyse segmented image, referencing calcium channel (can change)
			run("Set Measurements...", "area mean standard modal min integrated median redirect=C2-Image decimal=3");
			run("Analyze Particles...", "size=10-Infinity display exclude add");

			//Save ROI overlay to BF channel
			selectWindow("C6-Image");
			resetMinAndMax();
			roiManager("Show All");
			outName1 = "Mask_on_BF_" + imagesName;
			saveAs("tiff", outputDir + "/" + outName1);

			//Save ROI overlay to calcium channel
			selectWindow("C2-Image");
			resetMinAndMax();
			roiManager("Show All");
			outName2 = "Mask_on_Cal_" + imagesName;
			saveAs("tiff", outputDir + "/" + outName2);
		
			//Clear the ROI manager
			count = roiManager("count");
			if (count != 0)
			roiManager("delete");

			//Save and clear the results 
			saveAs("Results", outputDir + "/" + imagesName + "_Results.csv");
			run("Clear Results");

			//Close everything
			run("Close All");
  }
       		
